plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.mos'
version = '0.0.1-SNAPSHOT'

bootJar {
	archiveFileName = "mos.jar"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	asciidoctorExt
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.10.0'
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'

	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mysql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'com.auth0:java-jwt:4.2.1'
}

tasks.named('test') {
	useJUnitPlatform()
	outputs.dir snippetsDir
}

task createSnippetsDir {
	doLast {
		file(snippetsDir).mkdirs()
	}
}

asciidoctor {
	dependsOn test, createSnippetsDir
	configurations 'asciidoctorExt'
	sourceDir file('src/docs/asciidoc')
	inputs.dir snippetsDir
	outputDir file("build/docs/asciidoc")
}

// 변경된 부분: copyDocument에서 resources 디렉터리를 충돌하지 않도록 변경
task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("build/generated-resources/static/docs")  // build/resources/main이 아닌 별도의 폴더 사용
}

// processResources와 무관하게 copyDocument를 설정
build {
	dependsOn copyDocument
}

// 리소스 처리에 추가 디렉토리 포함 (스프링에서 제공할 수 있도록 설정)
sourceSets {
	main {
		resources {
			srcDir("build/generated-resources")
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// Clean 작업 시 추가 폴더 삭제
clean {
	delete file('src/main/generated'), file('build/generated-resources')
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}